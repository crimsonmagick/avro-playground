plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.0'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.mangomelancholy.avro.playground'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

configurations {
	avroTools
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.apache.avro:avro:1.11.1'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'

	// Avro Tools for manual compilation
	avroTools 'org.apache.avro:avro-tools:1.11.1'
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'build/generated/java'
		}
	}
}

task generateAvro {
	inputs.dir 'src/main/avro'
	outputs.dir 'build/generated/java'
	doLast {
		def avroFiles = fileTree(dir: 'src/main/avro', include: '**/*.avsc').files.collect { it.path }
		javaexec {
			main = 'org.apache.avro.tool.Main'
			classpath = configurations.avroTools
			args = ['compile', 'schema'] + avroFiles + ['build/generated/java']
		}
	}
}

compileJava.dependsOn generateAvro

tasks.named('test') {
	useJUnitPlatform()
}
